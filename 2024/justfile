set dotenv-load

var:
    #!/usr/bin/env sh
    echo YEAR="${YEAR:-$(basename $(pwd))}"

    DAY="${DAY:-$(find . -maxdepth 1 -type d -name 'day-*' | cut -d- -f2- | sort -nr | head -n 1)}"
    DAY="$(echo $DAY | sed -E 's/0*([0-9]*)/\1/')"
    echo DAY="$(printf '%d' $DAY)"
    echo DAY_DIR="$(printf 'day-%02d' $DAY)"

    PART="${PART:-1}"
    echo PART="$PART"
    echo PART_BIN="part${PART}"
    
alias c := create
create $DAY:
    eval `just var` && cargo generate --path ./daily-template --name "$DAY_DIR" && just input "$DAY"

alias i := input
input $DAY="":
    eval `just var` && \
    curl "https://adventofcode.com/${YEAR}/day/${DAY}/input" -b "session=$SESSION" -f > "./${DAY_DIR}/input.txt"

alias w := watch
watch $PART="" $DAY="":
    eval `just var` && cargo watch -w "$DAY_DIR" -x "check -p $DAY_DIR" -s "just test $PART $DAY" -s "just lint $DAY"

alias l := lint
lint $DAY="":
    eval `just var` && cargo clippy -p "$DAY_DIR"
    
alias f := fix
fix $DAY="":
    eval `just var` && cargo clippy -p "$DAY_DIR" --fix --allow-dirty

alias t := test
test $PART="" $DAY="":
    eval `just var` && cargo test -p "$DAY_DIR" --lib "$PART_BIN" --bin "$PART_BIN"

alias b := bench
bench $PART="" $DAY="":
    eval `just var` && cargo bench --bench "${DAY_DIR}-bench" "$PART_BIN" >> "./${DAY_DIR}/bench.txt"

alias r := run
run $PART="" $DAY="":
    eval `just var` && cargo run -p "$DAY_DIR" --bin "$PART_BIN"

alias y := yank
yank $PART="" $DAY="":
    just run $PART $DAY | xclip -sel clip -i

alias sm := submit
submit $PART="" $DAY="":
    #!/usr/bin/env sh
    eval `just var`

    ANSWER="$(cargo run -p $DAY_DIR --bin $PART_BIN)"

    curl -X POST -H "Content-Type: application/x-www-form-urlencoded" -d "level=${PART}&answer=${ANSWER}" \
    -b "session=$SESSION" -f "https://adventofcode.com/${YEAR}/day/${DAY}/answer"
